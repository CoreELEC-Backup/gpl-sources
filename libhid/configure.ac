#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_REVISION($Rev: 328 $)
AC_COPYRIGHT([Please see the ./COPYING file for license information.])

AC_PREREQ(2.57)

##### INITIALISATION #########################################################

# Making releases:
#     if functions were added, arg1 = 0
#     if not backward compatible, arg1 = arg2 = 0
#
m4_define([LIBHID_VERSION], [0.2.16])
m4_define([IFACE_AGE], [0])
m4_define([BIN_AGE],   [0])
m4_define([MD_INIT_NAME], [libhid])
m4_define([MD_INIT_VERSION], LIBHID_VERSION.IFACE_AGE.BIN_AGE)
m4_define([MD_INIT_ADDRESS], m4_esyscmd([grep -i bugreports\ to README | sed -e 's,.*<\([^>]*\).*,\1,' | tr -d "\n"]))

AC_INIT(MD_INIT_NAME, MD_INIT_VERSION, MD_INIT_ADDRESS)

AC_CONFIG_HEADERS([config.h])
AC_CONFIG_SRCDIR([include/hid.h])
AC_CONFIG_AUX_DIR([.])

AM_INIT_AUTOMAKE
AM_MAINTAINER_MODE

##### CONFIGURE ARGUMENTS ####################################################

MD_ARG_ENABLE_CUSTOM(pedantic, pedantic compiler checks, no)
MD_ARG_ENABLE_CUSTOM(warnings, proper compiler warnings, yes)
MD_ARG_ENABLE_CUSTOM(werror, compiler warnings as errors, yes)
MD_ARG_ENABLE_CUSTOM(debug, debugging, no)
MD_ARG_ENABLE_CUSTOM(gdbdebug, gdb-enhanced debugging symbols, no)

dnl ### If distcc and ccache are used via symlink farms (e.g. in
dnl ###/usr/lib/ccache), then the following checks fail.
dnl MD_ARG_WITH_CUSTOM(ccache, ccache for compiler caching, [yes, if found])
dnl MD_ARG_WITH_CUSTOM(distcc, distcc for distributed compilation, [yes, if found])

MD_ARG_WITH_CUSTOM(doxygen, documentation generated by doxygen, [yes, if found])

# TODO: move to with
MD_ARG_ENABLE_CUSTOM(swig, swig python wrapper, [yes, if found])

##### VERSIONING #############################################################

MD_VERSION_INFO
MD_LT_VERSION_INFO

PACKAGE=AC_PACKAGE_NAME
VERSION=$MD_VERSION

##### CONFIGURATION ##########################################################

MD_CHECK_OS

MD_CHECK_LIBUSB018B

AC_PROG_CC
MD_CHECK_GCC3

dnl MD_CONF_DISTCC
dnl MD_CONF_CCACHE

AC_PROG_LIBTOOL
AC_PROG_INSTALL
AC_PROG_MAKE_SET

AC_HEADER_STDC
AC_HEADER_STDBOOL

AC_C_INLINE
AC_C_CONST
AC_C_BIGENDIAN
AC_C_STRINGIZE

MD_CONF_DEBUGGING
MD_CONF_COMPILER
AM_CONDITIONAL(OS_LINUX, test "$MD_OS" = "linux")
AM_CONDITIONAL(OS_BSD, test "$MD_OS" = "bsd")
AM_CONDITIONAL(OS_SOLARIS, test "$MD_OS" = "solaris")
AM_CONDITIONAL(OS_DARWIN, test "$MD_OS" = "darwin")

if test "${ac_cv_enable_swig}" = "no" ; then
  AM_CONDITIONAL(HAVE_SWIG, false)
else
  AX_PKG_SWIG(1.3)
  AM_CONDITIONAL(HAVE_SWIG, "$SWIG" -version)
  if test -z "${HAVE_SWIG_TRUE}" ; then
    AM_PATH_PYTHON
    AX_SWIG_PYTHON
  fi
fi

MD_CHECK_DOXYGEN

MD_CONF_FLAGS(
  [],
  [$OS_CFLAGS $LIBUSB_CFLAGS],
  [],
  [$OS_LDFLAGS $LIBUSB_LIBS]
)

MD_CONF_DB2MAN

##### OUTPUT #################################################################

AC_CONFIG_FILES([Makefile])
AC_CONFIG_FILES([src/Makefile])
AC_CONFIG_FILES([hidparser/Makefile])
AC_CONFIG_FILES([swig/Makefile])
AC_CONFIG_FILES([test/Makefile])
AC_CONFIG_FILES([doc/doxygen.rc])
AC_CONFIG_FILES([doc/Makefile])
AC_CONFIG_FILES([doc/man/Makefile])
AC_CONFIG_FILES([doc/www/Makefile])
AC_CONFIG_FILES([pkgconfig/libhid.pc])

AC_OUTPUT

MD_DEBUG_BUILDVARS

# COPYRIGHT --
#
# This file is part of libhid, a user-space HID access library.
# libhid is (c) 2003-2005
#   Martin F. Krafft <libhid@pobox.madduck.net>
#   Charles Lepple <clepple@ghz.cc>
#   Arnaud Quette <arnaud.quette@free.fr> && <arnaud.quette@mgeups.com>
# and distributed under the terms of the GNU General Public License.
# See the file ./COPYING in the source distribution for more information.
#
# THIS PACKAGE IS PROVIDED "AS IS" AND WITHOUT ANY EXPRESS OR IMPLIED
# WARRANTIES, INCLUDING, WITHOUT LIMITATION, THE IMPLIED WARRANTIES
# OF MERCHANTIBILITY AND FITNESS FOR A PARTICULAR PURPOSE.

